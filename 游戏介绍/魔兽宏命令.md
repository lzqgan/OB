#game 
> 本文由 [简悦 SimpRead](http://ksria.com/simpread/) 转码， 原文地址 [ol.3dmgame.com](https://ol.3dmgame.com/gl/138009.html)

> 游戏《魔兽世界》中，使用辅助的工具能够提升我们自身的战斗力。很多玩家并不了解，那么宏命令从入门到精通怎么弄？一起来看宏命令教程从入门到精通吧。wow 宏命令教程从入门到精通

游戏《[魔兽世界](https://ol.3dmgame.com/wow/)》中，使用辅助的工具能够提升我们自身的战斗力。很多玩家并不了解，那么宏命令从入门到精通怎么弄？一起来看宏命令教程从入门到精通吧。

**wow 宏命令教程从入门到精通**

![](https://olimg.3dmgame.com/uploads/images/raiders/20210127/1611710146_668005.jpg)

问题：

魔兽世界宏命令从入门到精通怎么弄？

答案：

**正式服**

use 使用，cast 施放。过去非技能用 use 技能用 cast，现在他们功能是一样的，即便是长久的习惯和认知问题现在也应改过来，因为 use 3 个字节，中文命令 “施放”2 个字节，适用于精简语法，宏瘦身。

![](https://olimg.3dmgame.com/uploads/images/raiders/20210127/1611710146_877555.jpg)

use 有选鼠标选择功能，极少情况需要用到，如 13 和 14 装备栏位施放饰品宏 (6 和 15 腰带披风又可以用 “施放”)，怀旧服主副手上毒宏，选矿或开锁需要用 use。

目标判定 (仅有这 3 种目标判定，均可加 no 反向判定)

>harm 敌方， help 友方， exists 存在 (敌我通用技能用存在判断)

目标状态判定仅能判断是否死亡

>比如对活着的敌友或存在的目标施放加入 nodead; 复活技能判定加入 dead

对敌施放用 harm,nodead; 对友施放用 help,nodead

不加 nodead 会导致如鼠标指向一个死亡目标，仍然对此用技能一直提示错误目标，加入后就会忽略他执行下面的命令。例如组合键对焦点施放，焦点死亡判定不成立 > 往下鼠标指向 > 再往下当前目标

只能对敌人施放的技能用 harm，友方用 help，不要用 exists，导致问题同上，exists 用于敌我均能施放的技能，神圣视野，神圣震击

环境和状态判定 (均可加 no 反向判定，坐骑宏，环境，变形，姿态，潜行状态)

>indoors 室内，outdoors 室外，swimming 水里，flyable 可以飞，combat 战斗，stealth 潜行

group 队伍 (用于怀旧服整合技能简化技能数量，队伍状态豹群取代猎豹，群体上智耐祝福取代单体，也可自己 DIY 队伍和单人情况使用不同技能)

form 姿态 (form 比广泛使用的 stance 更节省字符应该改用，他们功能是完全一样的; 常用范例 form:X 姿态 X，form:X/X 姿态 X 或 X，noform 没有姿态，noform:X 没有姿态 X，noform:X/X 没有姿态 X 或 X)

[talent:X/X] 命令判定当前天赋，实现 2 个不同时点的天赋之间切换，可以是 1 天赋 1 技能之间，也可是 2 个天赋之间，只要不会冲突

需要说明 [PVPtalent:X/X] 在 8.0 大部分失效了不能用了! 天赋都混合在一起了，简单说现在 PVP 技能只能添加一个切换，后面有大量例子

[spec:X]判定当前专精，比如战士 [spec:1] 是武器，2 是狂暴，3 是防御; 把三系的多个宏使用该判定整合为一个宏，这个命令在宏整合非常重要

举例整合：三系基础技能自动切换，盾猛自动档加开关

#showtooltip

/ 施放 [spec:1,@mouseover,harm,nodead][spec:1] 致死打击;[spec:2,@mouseover,harm,nodead][spec:2] 嗜血;[mod,@mouseover,harm,nodead][mod] 盾牌猛击;[@mouseover,harm,nodead][] 盾牌猛击

/stopmacro [mod]

/ 施放 无视苦痛

/ 施放 盾牌格挡

简单讲就是这样，因排除法最后一个天赋不用加命令，下面这个宏没有任何判定也就拾取了做宏的意义

/ 施放 [spec:1] 奥术冲击;[spec:2] 火球术; 寒冰箭

@目标判定，player 自己，pet 宠物，focus 焦点，target 目标，targettarget 目标的目标，mouseover 鼠标指向，cursor 光标位置，form 姿态 (相比 stance 少 2 字符)，party1 队伍框架 1，arena1 竞技场敌方框架 1，partypet1 队伍框架 1 的宠物，arenapet1 竞技场敌方框架 1 的宠物

注意：focus 焦点，敌方死亡自动取消焦点，仍可以再次焦点尸体，友方死亡不会取消焦点，故对敌焦点施法宏不在加入 nodead 判定，例 [mod,@focus,harm]

@等于 target=

@XXXX 和 target=XXXX 是一个意思 (目标等于 XXXX)，所以用前面的 @节约 6 个字符，例如 @focus，@player，@pet 比较 target=focus，~=player，~=pet

注意:“@名字”“target = 名字” 这两个命令都只能选择队伍中的玩家，如果要选择敌方玩家和 NPC 用 “/ 目标 名字”

@mouseover 和 @Cursor 这两者适用于两者不同情况，他们是不一样的!

@mouseover 适用于鼠标指向施放技能

@cursor 适用于地板技能光标位置施放

@pet 目标宠物适用于需要了解目标状态施法的技能，比如猎人复活死亡的宠物

目标宠物 @pet 能判断自己的宠物，@targetpet 能判断队友的宠物或 @partypet1 队伍框架 1 玩家的宠物，@arenapet1 竞技场框架 1 位置敌方玩家的宠物。不能判定非组队的友方和场外敌方的宠物。就像某些技能只能对队友施放 (吐槽怀旧服牧师盾不能给队友之外，不识谁家犬也就算了，套个盾也要认识逻辑何在?)

; 分号，代表完整命令段落的结束，需要开启下一个段落; 用来分割，用于开始一个新的技能，同一个技能不需要; 分号，最后一个技能的末尾不需要

特殊情况如宝宝攻击 / petattack [mod,@focus,harm][@mouseover,harm,nodead]; 末端需要加分号或 [] 才会执行无指向的时候攻击当前目标 (这里; 分号 =[] 括号，区别是字节少 1 个)(带有技能名的情况; 分号≠[]括号)

同一个技能，多次 [目标判断] 只需要一个名字，一个分号; 例子

/ 施放 [mod,@focus,harm][@mouseover,harm,nodead][] 破胆怒吼

空格，做宏应当用最优化的逻辑节省尽量多的字符，只有起始命令如 / cast /use #showtooltip 等等加一空格才能生效，后续命令均是, 和; 来断句

每个 [] 都代表一个完整的判定，他是从上往下从左往右按顺匹配条件执行的，条件一般是组合键，敌我身份存活情况，天赋分辨姿态分辨

所有的 “/ 命令” 后面都可以加 [] 来添加条件，只要想得到且能解决问题

一些例子

/ 施放 [combat] 狂暴姿态; 战斗姿态 (战斗中切换狂暴姿态，非战斗战斗姿态，怀旧服)

/ 施放 [pet] 黑暗突变; 亡者复生 (没宝宝招宝宝，有宝宝变身)

/eq [nomod] 盾牌名字;(eq 是 equip 的缩写，没按组合键装备盾牌)

/equipslot [nomod]16 单手武器 (没按组合键装备 16 部位的单手武器)

/stopmacro [mod](按组合键停止该命令后面的命令)

/cancelform [mod](按组合键取消形态)

/cancelaura [mod] 技能 (按组合键取消技能)

/cleartarget [help][dead](不是敌人或死亡取消目标，如果刚才的 2 个分开的条件是在一个括号内 [noharm,dead] 就需要同时达成所有条件才执行命令)

按组合宠物移动，不按组合键施放技能，避免冲突

/petmoveto [mod]

/ 施放 [nomod] 技能

[] 括号内是判定条件，空括号是无判定即对目标施放，更简洁

/ 施放 [@mouseover,harm,nodead][] 吸血鬼之触

/ 施放 [@mouseover,harm,nodead] 吸血鬼之触; 吸血鬼之触

[] 有需要也可以加入组合键和目标判定，单方面的技能没必要在加判定，敌我通用技能看情况加判定，例子：

/ 施放 [@mouseover,harm,nodead][harm,nodead] 吸血鬼之触 (无意义)

/ 施放 [@mouseover,help,nodead][help,nodead] 神圣震击 (有意义)

! 宏技能名字前加感叹号，防止姿态被反复按取消，如 / cast ! 暗影形态，小德形态，消散冰箱等自带取消功能的技能

/stopmacro [] 停止宏 (括号里要加条件，达到条件中断这条命令以下的宏内)

比如 “/stopmacro [mod:alt/ctrl]” 按了 alt 或 ctrl 就不执行后面内容，“/stopmacro [nocombat]”没有战斗，“/stopmacro [harm,nodead]”敌方存活，诸如此类

“/stopmacro [@mouseover,noexists] [@mouseover,dead] ” 指向不存在或指向死亡不执行后面内容

* 注意：这些条件也可以加在技能上反向判定，是专门用停止宏加条件还是直接加在技能上面得看哪一种总字符用得少比如“/stopmacro [mod]”和 “/ 施放 [nomod]XX” 是一个功能，如果后者重复使用过多就超过停止宏字符，设计一个总开关更节约字符

设置焦点 (stopmacro 停止宏进行下面命令，鼠标指向通过的条件)

/focus

/stopmacro [@mouseover,noexists] [@mouseover,dead]

/focus mouseover

/targetenemy 目标敌人 (跟 TAB 一个机制，选正前方 90 度最远 50 码由近至远轮流流选目标，只会选视野能看到的目标，不会选遮挡的目标，所有角度内目标选完了重复此过程，DOT 职业有用)

/targetlasttarget 返回上一目标 (一直返回在两个目标间循环，配合上面的目标或配合指定 XXX 目标用，实现不切目标，像上面的例子)

/targetlastenemy 返回上一目标并锁定 (不会一直返回，锁定最后一个目标)

/stopcasting 停止施法 (反制冰箱无敌之类需要立即放出来的技能)

/stopcasting [nochanneling: 神圣赞美诗](不打断 [] 内指定技能，一般用于引导类技能，比如赞美诗苦修眼棱等)

/stopattack 停止自动攻击

* 变形 / 瘫痪类软控制指会被伤害打破的完全失去控制的技能，包括盲闷凿闪忏羊慰等，这类技能施放成功会自动停止攻击不需要额外加入命令

* 注意瘫痪类道具如手雷不会自动停止攻击

/startattack 自动攻击 (选择最近目标并锁定用武器近战攻击)

* 技能类型和效果：技能类型分打击技能和法术技能，技能效果分物理和魔法，例如 DK 窒息是法术技能物理效果会被保护免疫，DH 邪能爆发是法术技能魔法效果会被反魔法护盾免疫，ZS 风暴之锤是打击技能物理效果，QS 裁决是打击技能魔法效果，QS 妨害是法术技能魔法效果等等

* 注意：1. 打击技能会优先开启自动攻击 / startattack 选择最近的目标并锁定后才会尝试施放技能，法术技能不会开启自动攻击，所有近战的打断包括 DK 和 DH 的远程打断是打击技能不是法术技能，所有法系打断是法术技能 (怀旧服猎人射击技能会开启自动射击，射击技能和自动射击不会自动选最近怪锁定)

2. 控制技能例如 DK 窒息 DH 邪能爆发是法术技能不会开启自动攻击，ZS 风暴之锤是打击技能会开启自动攻击，邪能爆发和风暴之锤都是远程控制技能且都有伤害但是类别却不同跟是否有伤害无关

3. 法术技能 (和怀旧服猎人射击技能) 前是否需要加入 / startattack 来协助选择目标或是后续接普攻 (法系捅图腾，偷伤害) 减少损失有细微的作用

/startattack [nostealth](潜行中禁止普攻，这个条件对贼德潜行起手技能加常规技能的组合宏很有必要)

怀旧服魔杖射击，指向优先 (秒图腾!)

/ 施放 [@mouseover,harm,nodead][]! 射击

/petattack 宠物攻击 (宠物攻击命令)

组合键宠物攻击焦点敌人 > 宠物攻击指向敌人 > 宠物攻击目标敌人

/petattack [mod,@focus,harm][@mouseover,harm,nodead];

怀旧服没焦点，其他同上

/petattack [@mouseover,harm,nodead];

/petstay 宠物停留 (留在原地，并非停止攻击，行为和姿态有关)

/petfollow 宠物跟随 (停止行为回到指定地点> 跟着你)

/petmoveto 宠物移动 (宠物移动到鼠标指定位置; 正式服功能)

/petassist 宠物协助 (是姿态; 同你一起攻击; 正式服功能)

/petpassive 宠物被动 (是姿态; 停止行为回到指定地点> 跟着你，什么都不做)

/click PetActionButtonX (是脚本，字符占用太多仅列出不推荐; 点击宠物动作条第 X 位置，X 改成对应的位置)

/dismount 下坐骑

/f 名字 (跟随名字)

你可以在这些命令后面加 [] 目标条件或按键条件，比如按组合键宝宝跟随“/petfollow [mod]”，目标是敌人下坐骑“/dismount [harm]”

/cleartarget (清除目标，一般配合选择目标怀旧服刷怪用，下例不选中非敌人和死亡)

/ 目标 名字

/cleartarget [help][dead]

/ 施放 技能

/cancelform 取消变形 (战士小德怀旧服暗牧专用，加条件例如 / cancelform [form:2/4] 取消 2 和 4 形态)

/cancelaura 取消 BUFF(例如 / cancelaura 寒冰屏障)

[]括号，添加判定条件，状态，敌友，焦点，指向，按键等 (例如 / cancelaura [mod] 寒冰屏障，按组合键取消)

[nochanneling: 苦修] 不中断指定引导技能，不添加技能名字 [nochanneling] 会取消所有引导技能

例子盾不打断苦修：/ 施放 [nochanneling: 苦修] 真言术：盾

* 适用于奥术飞弹，术士吸取，猎人治疗，死亡射线等引导技能或道具

姿态判定 form

例 1：不在姿态就进入姿态

#showtooltip

/startattack

/ 施放 [noform:1] 暗影形态;[mod,@focus,harm][@mouseover,harm,nodead][] 吸血鬼之触

例 2：按指定组合键不在该变形就变形 > 在形态就冲锋指向 > 无指向冲目标

#showtooltip [@mouseover,exists,nodead][] 野性冲锋

/ 施放 [mod:alt,noform:1] 熊形态;[mod:shift,noform:2] 猎豹形态;[mod:ctrl,noform:4] 枭兽形态;[@mouseover,exists,nodead][] 野性冲锋

[换装](https://ol.3dmgame.com/tag/dnfhztj/)备指令

/eq(eq 是 equip 的缩写) 和 / equipslot(无法缩写)

/eq 装备名字，把装备穿在默认的位置，类似右键穿戴

/equipslot 栏位编号 (空格) 装备名字，指定穿戴，适用戒指饰品 2 个栏位

全身编号：1 头，2 项链，3 肩，4 衬衣，5 胸，6 腰带，7 腿，8 脚，9 手腕，10 手套，11 戒指 1，12 戒指 2，13 饰品 1，14 饰品 2，15 披风，16 主手，17 副手

例：/eq 协同披风 /equipslot 13 惊恶角斗士的咒徽

一切 “script(脚本)” 和“run(运行)”英文都应用中文 “脚本” 代替节约 1-4 个字符(脚本包含运行含义)

例如下面书写格式有 script 也有 run，很混乱

/script VehicleExit() 下载具，比如炮，座位载具

/run PetDismiss() 解散宠物

全部统一用中文 “脚本” 替换

/ 脚本 VehicleExit()

/ 脚本 PetDismiss()

常用游戏设定命令，聊天框输入命令运行，0 是关闭，1 是开启

(/console 是控制台命令，聊天框输入且重登无效时，需用文本打开 WTF\Config.wtf 手动添加)

/console SET overrideArchive "0" 反和谐 (除了图标)

/console SET profanityFilter "0" 关文字过滤

/console SET ffxGlow "0" 关泛光效果 (关闭光线反射会使色彩变暗，建议不关)

/console SET ffxDeath "0" 关死亡效果，黑白变彩色

/console SET weatherDensity "0" 天气效果 0-3 档由弱变强 (降低暴雨，风沙，雾气)

/console SET chatClassColorOverride "0" 聊天框显示职业颜色

/console SET targetNearestDistance "50" 改 TAB 距离 0-50 码

/console SET targetNearestDistanceRadius "50" 改 TAB 身后距离 0-50 码，默认是 10 码

敌方血条显示命令

/ 脚本 SetCVar("nameplateShowEnemyGuardians",0) 守护者

/ 脚本 SetCVar("nameplateShowEnemyMinions",0) 仆从

/ 脚本 SetCVar("nameplateShowEnemyPets",0) 宠物

/ 脚本 SetCVar("nameplateShowEnemyTotems",0) 图腾

/run SetCVar("nameplateShowEnemyMinus",0) 次要

友方血条显示命令

/ 脚本 SetCVar("nameplateShowFriendlyGuardians",0) 守护者

/ 脚本 SetCVar("nameplateShowFriendlyMinions",0) 仆从

/ 脚本 SetCVar("nameplateShowFriendlyNPCs",0) NPC

/ 脚本 SetCVar("nameplateShowFriendlyPets",0) 宠物

/ 脚本 SetCVar("nameplateShowFriendlyTotems",0) 图腾

镜头距离最高 2.6 自行调试

/ 脚本 SetCVar('cameraDistanceMaxZoomFactor', 2.6)

姓名板大字体姓名取代血条

/ 脚本 SetCVar("nameplateShowOnlyNames",0)

非当前目标血条透明度，尾数自改

/ 脚本 SetCVar("nameplateMinAlpha",0.7)

重置服务器设定 (慎用! 疑难杂症无解时的最后办法)

/console cvar_default

以上收录常见问题需要用到的功能，因为插件会修改设定，并不会还原，所以经常会遇见这类问题

回归本文主要讲常规宏，顺便收录一些实用的 LUA 命令

#showtooltip 显示图标提示

#show 显示图标 (不会显示使用说明，除非字符不够才用这个)

在后面加入天赋判断命令 [talent:x/x]，目标判断命令[nopet] 召唤宝宝;[pet]宝宝技能，加入指向判定等等能显示距离判定超出距离会变红

下面举例描述如何做到距离显示，焦点和指向的距离显示，自动切换天赋技能，

重点：暴雪默认的技能超出距离图标变红作用于当前目标和按下焦点和指向施法键的时候; 在加入技能名字固定显示后只有当前目标生效，焦点和指向的距离判断无效了，所以只有在逻辑上通过不了必须加才能监视的情况下加

这个技能不需要距离显示，因为饰品序列在前面所以加了技能才能监视

#showtooltip 绝望祷言

/use 13

/ 施放 绝望祷言

/ 施放 [@player] 真言术：盾

/ 施放 [@player] 圣言术：静

石像鬼是需要距离显示的，因为饰品序列在前面所以加了技能和判断条件才能监视，不过这类持续伤害技能生效期开饰品也一样，可以把饰品放在最后简化字符

#showtooltip [talent:7/2] 邪恶狂乱;[mod,@focus,harm][@mouseover,harm,nodead][] 召唤石像鬼

/use 13

/startattack

/ 施放 邪恶狂乱

/ 施放 [mod,@focus,harm][@mouseover,harm,nodead][] 召唤石像鬼

简化之后

#showtooltip

/startattack

/ 施放 [talent:7/2] 邪恶狂乱

/ 施放 [mod,@focus,harm][@mouseover,harm,nodead][] 召唤石像鬼

/use 13

2 个不会同时点的天赋，给一个技能的三种施放条件 (焦点，指向，目标) 用[talent:x/x]排除法更简约字符

组合键焦点友方 > 指向友方 > 目标友方

#showtooltip

/ 施放 [talent:5/2,mod,@focus,help,nodead][talent:5/2,@mouseover,help,nodead][talent:5/2] 联结治疗;[mod,@focus,help,nodead][@mouseover,help,nodead][] 治疗之环

同上

组合键焦点敌方 > 指向敌方 > 目标敌方

#showtooltip

/startattack

/ 施放 [talent:3/3,mod,@focus,harm][talent:3/3,@mouseover,harm,nodead][talent:3/3] 黑暗虚空;[mod,@focus,harm][@mouseover,harm,nodead][] 虚空洪流

由于主要监视技能在最后面 #showtooltip 要想显示野性冲锋的距离判定，需要把他的施放条件 (指向，目标) 也加进来

按指定组合键不在该变形就变形 > 在形态就冲锋指向 > 无指向冲目标

#showtooltip [@mouseover,exists,nodead][] 野性冲锋

/ 施放 [mod:alt,noform:1] 熊形态;[mod:shift,noform:2] 猎豹形态;[mod:ctrl,noform:4] 枭兽形态;[@mouseover,exists,nodead][] 野性冲锋

上面介绍完宏的基础，下面是一些运用案例，需要强调一句，写宏不要死搬硬套，注意从上往下从左往右的语句逻辑，可以节约很多字符，可能你不去节约 255 字符也不会爆炸，但是应该把他当成创作去追求完美，养成良好的逻辑习惯去简化语句，不要拖泥带水藏污纳垢

一个一元宏和一个多元宏他们的判断条件都是完整的，但多元宏根据队列逻辑可以使用排除法越往后选择越少也就不需要条件了

三个组合键 shift,ctrl,alt，如果三个都用上的情况那最后一个因排除法是不需要注明哪个键的，而最后一个必定是 shift 因为他 5 个字符;

举个例子

/ 施放 [mod:alt] 魔甲术;[mod:ctrl] 奥术光辉;[mod] 奥术智慧; 冰甲术

/ 施放 [pet] 黑暗突变; 亡者复生 (没宝宝招宝宝，有宝宝变身)

/ 施放 [combat] 狂暴姿态; 战斗姿态 (战斗中切换狂暴姿态，非战斗战斗姿态，怀旧服)

利用好敌友判定可以把 1 个宏做 3 + 个技能外加 3 个组合键，X 种条件进去，节约键位，高度智能，例子是 6 个判定 3 个组合键：

#showtooltip

/ 施放 [mod:alt,@targettarget,harm,nodead][@mouseover,harm,nodead][harm,nodead] 驱散魔法;[mod:ctrl] 群体复活; [@mouseover,help,dead] [help,dead] 复活术;[mod,@player][@mouseover,help,nodead][] 纯净术

智能按环境上下坐骑自动拾取上 BUFF，宝宝跟随，停止攻击，取消变形

附近有拾取优先拾取 (拾取可以改成上 BUFF，想同时存在其中一个要改成组合键)，组合键清包包修理三人坐骑> 飞行坐骑 = 水下坐骑>陆地坐骑

#showtooltip 坐骑名字

/ 脚本 VehicleExit()

/dismount

/petfollow

/stopattack

/cancelform

/ 施放 玩具

/ 施放 远程拾取器

/sp 小宠物

/ 施放 [mod] 雄壮远足牦牛;[swimming] 水下坐骑;[flyable] 飞行坐骑; 陆地坐骑

智能饰品：地板光标，组合键焦点 > 鼠标指向 > 当前目标

#showtooltip

/ 施放 [mod,@focus,exists,nodead][@mouseover,exists,nodead] [@cursor] []13

正式服组合键切换饰品 + 智能施放

#showtooltip

/eq [mod:atl] 饰品 1;[mod:ctrl] 饰品 2;[mod] 饰品 3

/ 施放 [mod,@focus,exists,nodead][@mouseover,exists,nodead] [@cursor] []13

怀旧服组合键切换饰品 + 智能施放

/eq [mod:atl] 饰品 1;[mod:ctrl] 饰品 2;[mod] 饰品 3

/use [@mouseover,exists,nodead] [@cursor] []13

* 注意：怀旧服装备栏位数字编号使用物品需要用 / use，直接用物品名字才可以用 / 施放，怀旧服没有焦点

手雷或物品指向优先，组合键焦点施放，再次按打断手雷施法重新施放

#showtooltip

/startattack

/stopcasting

/ 施放 [mod,@focus,exists,nodead][@mouseover,exists,nodead] [@cursor] [] 铁皮手雷

怀旧服去除焦点，加入宠物跟随，停止攻击，取消变形

#showtooltip

/petfollow

/startattack

/stopattack

/cancelform

/stopcasting

/ 施放 [@mouseover,exists,nodead] [@cursor] [] 铁皮手雷

吃喝用上药五合一宏

#showtooltip

/ 施放 [mod:atl] 食物 BUFF;[mod:shift] 职业 BUFF;[mod:ctrl] 敲鼓卷轴;[combat] 药; 魔法汉堡

/ 脚本 BuyMerchantItem(12,180)

这个宏是购买 180 个 NPC 第 12 个位置 (也就是第二页第一行右边) 的东西

没宝宝召唤宝宝，有宝宝放技能 (指向技能可加命令)，同时宝宝攻击

#showtooltip

/startattack

/ 施放 [nopet] 招宝宝;[mod,@player] [@cursor] 水宝宝冰环

/petattack [mod,@focus,harm][@mouseover,harm,nodead];

DK 版由于宝宝有 2 个技能，可以 2 个宏搭配

1 没宝宝招宝宝，有宝宝黑暗突变

#showtooltip

/startattack

/ 施放 [pet] 黑暗突变; 亡者复生

/petattack [mod,@focus,harm][@mouseover,harm,nodead];

2 大于 5 码宝宝冲锋，小于 5 码宝宝晕

#showtooltip

/startattack

/ 施放 [mod,@focus,harm][@mouseover,harm,nodead][] 撕扯

/ 施放 [mod,@focus,harm][@mouseover,harm,nodead][] 跳跃

/petattack [mod,@focus,harm][@mouseover,harm,nodead];

DKSS 抓宠物解散宠物，在坐骑上就下坐骑解散

#showtooltip

/dismount

/startattack

/ 脚本 PetDismiss()

/ 施放 [mod,@focus,harm][@mouseover,harm,nodead][] 控制亡灵

占用一个格子的天赋变换宏，用 [talent:X/X] 命令，可以跨天赋组合

#showtooltip

/ 施放 [talent:1/3,@mouseover,harm,nodead][talent:1/3] 暗影之爪

;[@mouseover,harm,nodead][] 死疽打击

一格组合键两技

/ 施放 [mod,@mouseover] 宝宝晕; [@mouseover] 自己晕

焦点目标的目标施法

/ 施放 [mod,@focustarget,harm,nodead][@mouseover,harm,nodead][] 技能

施法队列

/castsequence 是施法队列宏

后边接技能队列

/castsequence reset=10/target/combat 技能 1, 技能 2, 技能 3

reset 部分是重置条件, 走完队列重置, 放技能 10 秒闲置后重置 (读条技能是按开始读条时计算), 切目标重置, 脱离战斗重置

例 1：ctrl 群活，alt 驱自己，shift 驱焦点 > 指向敌人队列打人，友方驱散，死亡复活 > 目标敌人队列打人，友方驱散，死亡复活

#showtooltip

/castsequence [@mouseover,harm,nodead]reset=5 审判, 十字军打击, 十字军打击

/ 施放 [mod:ctrl] 宽恕; [@mouseover,help,dead] [help,dead] 救赎;[mod:alt,@player][mod,@focus,help,nodead][@mouseover,help,nodead][] 清洁术

例 2：组合键指向月火 > 组合键目标月火 > 指向队列施法 > 目标队列施法; 换目标，脱战，5 秒闲置重置

#showtooltip

/startattack

/ 施放 [mod,@mouseover,harm,nodead][mod] 月火术

/castsequence [@mouseover,harm,nodead][]reset=5/target/combat 斜掠, 月火术

例 3：队列烈焰风暴类地板技能，组合键自己脚下 > 鼠标位置 > 鼠标指向 > 目标; 5 秒闲置重置

/castsequence [mod,@player] [@cursor] [@mouseover,harm,nodead][]reset=5 技能 1, 技能 2, 技能 3

德鲁伊同层天赋，组合键变形 > 某个天赋 (如在施法打断施法，需连按 2 次施放)

#showtooltip

/stopcasting

/ 施放 [talent:2/1] 猛虎冲刺;[talent:2/2] 甘霖;[mod:ctrl,nostance:1] 熊形态;[mod:alt,nostance:3] 旅行形态;[@mouseover,harm,nodead][] 野性冲锋

shift 焦点友方，ctrl 指向敌方 > ctrl 目标敌方 > 指向友方 > 目标友方 > 锁定自己 (避免失误用于敌方)

#showtooltip

/ 施放 [mod:shift,@focus,help,nodead][mod:ctrl,@mouseover,harm,nodead][mod:ctrl,harm,nodead][@mouseover,help,nodead][help,nodead] [@player] 神圣震击

治疗输出敌方判定，防止打断引导

#showtooltip

/ 施放 [@targettarget,harm,nodead,nochanneling][harm,nodead,nochanneling] 惩击

治疗目标和自身

#showtooltip

/ 施放 [mod,@player][@mouseover,help,nodead][] 暗影愈合

牧师 5 判定驱和解 (复活 + 群活也做进来，节省 2 个偏远键位)

治疗例子：shift 驱散目标的敌人，ctrl 群体复活，alt 驱自己 > 指向敌方驱散，指向友方纯净，指向死亡友方复活 > 目标敌方驱散，目标友方驱散，目标友方死亡复活

#showtooltip

/ 施放 [mod:shift,@targettarget,harm,nodead][@mouseover,harm,nodead][harm,nodead] 驱散魔法;[mod:ctrl] 群体复活; [@mouseover,help,dead] [help,dead] 复活术;[mod,@player][@mouseover,help,nodead][] 纯净术

输出例子：暗牧推荐 (解与驱的键位是高级别反应的，这个宝贵的键位应该更智能化，由于 PVP 驱散疾病不如萨满驱散妖术有意义，但 PVE 又需要用到，使用率很低又不想占位，驱散仍然可用，主要友方指向改成信仰飞跃，拯救被炸飞的队友是关键)

alt 驱指向友方，ctrl 复活 (对释放尸体用)，shift 拉焦点的友方目标> 指向敌方驱散，指向友方拉人，指向友方复活>目标敌方驱散，目标友方拉人，目标友方死亡复活

#showtooltip

/ 施放 [mod:alt,@mouseover,help,nodead] 净化疾病;[mod:ctrl] [@mouseover,help,dead] [help,dead] 复活术;[@mouseover,harm,nodead][harm,nodead] 驱散魔法;[mod,@focustarget,help,nodead][@mouseover,help,nodead][] 信仰飞跃

一些示范，需要注意队列逻辑顺序，同样可用条件下优先执行前面的

例如：鼠标指向友方驱散 = 友方死亡复活 > 组合键打断焦点 > 指向敌方打断 > 目标打断

如果同时指向友方按组合键是不能打断焦点的，即队列逻辑顺序

#showtooltip

/ 施放 [mod:alt,@player][@mouseover,help,nodead][help,nodead] 清毒术;[mod:ctrl] [@mouseover,help,dead] [help,dead] 救赎;[mod,@focus,harm][@mouseover,harm,nodead][] 责难

可以改队列逻辑顺序，修改优先顺序或添加额外判定，比如脱战判定 nocombat 通过才施放复活，下例就优先焦点了，除了多用字符外实战中没必要，因为好的宏是会避开冲突点的减少判断需要的条件从而简化字符

#showtooltip

/ 施放 [mod:shift,@focus,harm] 责难;[mod:alt,@player][@mouseover,help,nodead][help,nodead] 清毒术;[mod] [@mouseover,help,dead] [help,dead] 救赎;[@mouseover,harm,nodead][] 责难

组合键，指向，当前友方，当前敌方

#showtooltip

/ 施放 [mod,@player,nochanneling][@mouseover,help,nodead,nochanneling][help,nodead,nochanneling] 真言术：耀;[harm,nodead,nochanneling] 净化邪恶

指向友方 > 目标友方换血，插战旗，吃糖; 组合键焦点敌方 > 指向敌方 > 目标敌方沉默

#showtooltip

/ 施放 [mod:ctrl][@mouseover,help,nodead][help,nodead] 虚空转移;[mod,@focus,harm][@mouseover,harm,nodead][] 沉默

/ 施放 [@mouseover,help] [help] 部落军旗

/ 施放 [@mouseover,help] [help] 治疗石

按 1 下取消剑刃风暴>组合键战斗怒吼>鼠标指向打断，同时指向用别的技能>目标打断，同时目标用别的技能>距离不够指向冲锋>距离不够目标冲锋 (疑问：盾牌猛击必须在斩杀前面才能生效。多一个“/ 施放” 比多 2 个 “[spec:X]” 节省 9 个字符)

#showtooltip

/cancelaura 剑刃风暴

/ 施放 [mod] 战斗怒吼; [@mouseover,help,dead] [help,dead] 终极版侏儒军刀;[@mouseover,harm,nodead][] 拳击

/ 施放 [@mouseover,harm,nodead][] 盾牌猛击

/ 施放 [@mouseover,harm,nodead][] 斩杀

/ 施放 [@mouseover,exists,nodead][] 冲锋

ctrl 看复活 CD，对目标复活; 组合键敌方打断 > 指向友方死亡战斗中战复，非战斗电击; 指向敌方打断 > 目标友方死亡战斗中战复，非战斗电击; 目标敌方打断

#showtooltip

/ 施放 [mod:ctrl][@mouseover,help,dead,combat][help,dead,combat] 复活盟友; [@mouseover,help,dead] [help,dead] 终极版侏儒军刀;[mod,@focus,harm][@mouseover,harm,nodead][] 心灵冰冻

DH 滑翔速度固定 178%，理论上 DH 在 PVP 应一直按空格，在某些不冲突的技能绑定 “/ 施放 ! 滑翔”，! 防止取消滑翔形态，绑定宏能增加移动速度加成的覆盖率，比不停按空格 3 次才能触发滑翔效率，保护双手

PVP 和 PVE 不同时点技能的整合宏，组合键自己脚下技能 > 光标处技能 = 指向伊利丹之握 > 目标伊利丹之握 (伊利丹之握 2 段施放，1 断和常规指向或目标技能一样，2 段和地板技能一样)

#showtooltip

/startattack

/ 施放 [talent:5/3,mod,@player][talent:5/3,@cursor] 锁链咒符;[@mouseover,harm,nodead][mod,@player] [@cursor] [] 伊利丹之握

DH 刃舞和烈焰符咒随天赋自动切换

*DH 这 2 个不同专精的技能比较特殊，他们是共通的会自动切换可用的就像用了 [spec:X] 命令来实现切换; 只有名称是刃舞时烈焰咒符才会生效

#showtooltip 刃舞

/startattack

/ 施放 [mod,@player] [@cursor,nochanneling] 刃舞

DH 冲刺和大跳随专精自动切换

*DH 这 2 个不同专精的技能比较特殊，他们是共通的会自动切换可用的就像用了 [spec:X] 命令来实现切换; 只有名称是邪能冲撞时地狱火撞击才会生效

#showtooltip

/startattack

/ 施放 [mod,@player] [@cursor] 邪能冲撞

DH 混乱打击和灵魂裂劈随专精自动切换; 添加防打断引导

*DH 这 2 个不同专精的技能比较特殊，他们是共通的会自动切换可用的就像用了 [spec:X] 命令来实现切换; 只有名称是混乱打击时灵魂裂劈才会生效

#showtooltip

/ 施放 [@mouseover,harm,nodead,nochanneling][nochanneling] 混乱打击

*DH 专精共通技能另外还有疾影和嘲讽

三个不会同时点的天赋之间自动切换

#showtooltip

/startattack

/ 施放 [talent:3/3,mod,@focus,harm][talent:3/3,@mouseover,harm,nodead][talent:3/3] 邪能之刃;[talent:6/2] 幽魂炸弹; 邪能毁灭

猎人招宝宝 > 宠物协同 > 杀戮命令

#showtooltip

/startattack

/petfollow (脱战后被动可能会停在原地，跟随是复写被动命令; 另外在无目标时作为单独使用会跟随你)

/petpassive (宝宝被动，停止行为> 等待指令，习惯用协助可以改 / petassist 在这个宏里没有区别，看你习惯常驻什么姿态)

/petattack [mod,@focus,harm][@mouseover,harm,nodead];

/ 施放 [nopet] 召唤宠物;[mod,@focus,harm][@mouseover,harm,nodead][] 杀戮命令

(下面一些功能可能用不上，其他职业修改一下取长补短，配合钓鱼, 传送, 某技能宏简化日常生活，加入宏终止避免换装或钓鱼同时放技能引到怪)

整合宏：组合键换武器 > 战斗复活 > 脱战复活; 某技能焦点 > 指向 > 目标

#showtooltip

/eq [mod:alt] 武器名字

/stopmacro [mod:alt]

/ 施放 [mod:ctrl][@mouseover,help,dead,combat][help,dead,combat] 复活盟友; [@mouseover,help,dead] [help,dead] 终极版侏儒军刀;[mod,@focus,harm][@mouseover,harm,nodead][] 心灵冰冻

整合宏：组合键换鱼竿，钓鱼，传送鱼点; 某技能焦点 > 指向 > 目标

#showtooltip

/eq [mod:alt] 幽光鱼竿

/ 施放 [mod:alt] 钓鱼;[mod:ctrl] 暗流

/stopmacro [mod:alt/ctrl]

/startattack

/ 施放 [mod,@focus,harm][@mouseover,harm,nodead][] 寒冰锁链

示范 2：如果不需要组合键焦点施法或不需要传送鱼点，即可全整合在一起

组合键换鱼竿，武器，传送鱼点; 某技能指向 > 目标

#showtooltip

/eq [mod:alt] 幽光鱼竿

/eq [mod:shift] 武器名字

/ 施放 [mod:alt] 钓鱼;[mod:ctrl] 暗流

/stopmacro [mod]

/startattack

/ 施放 [@mouseover,harm,nodead][] 技能

复活密语提醒

#showtooltip

/ 施放 [@mouseover,help] [] 技能

/ 脚本 SendChatMessage("%T 密语内容 ","whisper",GetDefaultLanguage("target"),UnitName("target"))

战士 T,DHT, 熊自动档

组合键只放盾牌猛击优先指向 > 直接按盾牌猛击，能量 =>40 优先同时无视痛苦，能量 =>30,40 < 同时格挡

#showtooltip

/ 施放 [mod,@mouseover,harm,nodead][mod] 盾牌猛击;[@mouseover,harm,nodead][] 盾牌猛击

/ 施放 [nomod] 无视苦痛

/ 施放 [nomod] 盾牌格挡

点评：逻辑优化后接近手动质量，盾牌格恶魔尖刺这类有层数的技能，虽然 2 次施放持续叠加不会溢出，但是两次连续施放后面 CD 会有压力，适合点强力反击天赋避免自动档的问题 (战士一般能延长 2-3 秒即 33-50% 时间，类似 DH 点减尖刺 CD)，无视痛苦是刷新无法叠加，在敌方未攻击的时候用组合键存能量 (实验证明优先无视痛苦可以让盾挡真空期更分散避免没点强力反击天赋因连续施放盾挡导致真空期过大)

三系基础技能自动切换整合版，其他功能同上

#showtooltip

/ 施放 [spec:1,@mouseover,harm,nodead][spec:1] 致死打击;[spec:2,@mouseover,harm,nodead][spec:2] 嗜血;[mod,@mouseover,harm,nodead][mod] 盾牌猛击;[@mouseover,harm,nodead][] 盾牌猛击

/ 施放 [nomod] 无视苦痛

/ 施放 [nomod] 盾牌格挡

脱战指向陷阱，战斗中指向陷阱同时假死

点评: 指向冰冻陷才能瞬发，才能和后面的假死同时施放

/ 施放 [mod,@player] [@cursor] 冰冻陷阱

/stopmacro [nocombat]

/ 施放 假死

应简化为

/ 施放 [mod,@player] [@cursor] 冰冻陷阱

/ 施放 [combat] 假死

按顺序施法，焦点 > 鼠标指向 > 目标的目标 > 宝宝 > 目标

#showtooltip

/ 施放 [@focus,help] [@mouseover,help,nodead] [@targettarget,help] [@pet,exists] [] 技能

指定队友施法 (只能是队伍或团队中的玩家)

#showtooltip

/ 施放 [@她的名字] 技能

目标宏，仅选中活生生的敌方目标

/ 目标 名字

/cleartarget [noharm][dead]

没宝宝召唤宝宝，同时按宝宝技能栏 5

点评：和前面法师水元素 DK 食尸鬼不同，加入宝宝键位栏指定按键可以配合猎人召唤不同宝宝的对应技能，比添加多个技能名好在能显示正常

#showtooltip

/ 施放 [nopet] 召唤宝宝

/click PetActionButton5

秒消 BUFF，战场秒丢旗帜，巫妖解控制，无尽牺牲控血，秒保护解状态，各种飞行状态

/cancelaura 巫妖之躯

/cancelaura 牺牲祝福

/cancelaura 保护祝福

/cancelaura 圣盾术

/cancelaura 寒冰屏障

/cancelaura 地精滑翔器

/cancelaura 降落伞

/cancelaura 缓落术

/cancelaura 漂浮术

/cancelaura 联盟旗帜

/cancelaura 部落旗帜

/cancelaura 联盟矿车

/cancelaura 部落矿车

/cancelaura 虚空风暴旗帜

一大堆我在用的宏，数次更新迭代，头脑风暴的成果

战士

自动专精，自动天赋，自动饰品

#showtooltip

/startattack

/ 施放 [spec:1,talent:6/2] 致命平静; 天神下凡

/use [mod,@focus,exists,nodead][@mouseover,exists,nodead] [@cursor] []13

自动剑刃破坏者，自动饰品

#showtooltip

/cancelaura [mod] 剑刃风暴

/ 施放 [mod,@player] [@cursor] 剑刃风暴

/use [mod,@focus,exists,nodead][@mouseover,exists,nodead] [@cursor] []13

骑士

shift 焦点友方，ctrl 鼠标指向 > 目标敌方，鼠标指向 > 目标友方，目标非友方自己

#showtooltip

/ 施放 [mod:shift,@focus,help,nodead][mod:ctrl,@mouseover,harm,nodead][mod:ctrl,harm,nodead][@mouseover,help,nodead][help,nodead] [@player] 神圣震击

牧师

三选一

#showtooltip

/ 施放 [talent:6/2] 神圣之星;[talent:6/3] 光晕;[mod:shift,@focus,help,nodead][@mouseover,help,nodead][] 神圣守卫

二连

#showtooltip

/castsequence reset=15 神圣之星, 治疗之环

![](https://olimg.3dmgame.com/uploads/images/raiders/20210127/1611710146_457182.jpg)

**怀旧服宏**

补充技能的运用，还是和正式服一样追求多功能智能化，精益求精的语法。

基础内容建议先看主贴，怀旧服和和正式服语法完全一样，此贴仅作为怀旧服技能运用的补充。

**和正式服不同**

正式服; 施放近战攻击技能 (例如致死打击，旋风斩) 会选择目标>同时自动攻击>同时施放技能(例如 / startattack 写在技能前面一样效果)

怀旧服; 施放近战攻击技能不会选择目标，只有在有资源施放时才会自动攻击 > 同时施放技能

**和怀旧服相同**

普攻和技能同时施放，普攻在技能前生效，也可能是怀旧服 200 毫秒反馈判定造成和正式服相同的假象

强调这个不同意在宏的近战攻击技能前需要加入 / startattack，有无资源施放都会自动攻击

**怀旧服一些额外说明**

起手技能一般需要同时绑定起立，下坐骑命令

起立 (坐下情况放技能第一次是起立，绑定这个命令按第一次就能放出技能)

/stand

下坐骑 (骑马状态需要先下坐骑才能施放技能，绑定这个命令按第一次是下马)

/dismount

小德变形普攻 CD 会按百分比延续，不会重新计时

换武器会重新计时普攻 CD

战斗中换武器会有 1.5 秒 GCD

招架使你本次近战攻击总量间隔降低 40%(剩余不足 40% 会立刻普攻，怪物也是)，可以理解为 1% 招架 = 1% 闪避 + 0.4% 攻速 X 怪物数量，战骑 T 战斗贼招架收益最大。

圣骑士伤害圣印会触发击中时 XX 的附魔，

满级 1W 经验 = 6G，奥山交牌子 = 5G

第一个例子啰嗦一点，以后的例子都遵循这种精简语法

小德全形态精灵之火，自动取消旅行形态

#showtooltip

/startattack

/cancelform [form:2/4]

/ 施放 [noform] 精灵之火; 精灵之火 (野性)

#showtooltip

/startattack

/cancelform [form:2/4]

/ 施放 [noform,@mouseover,harm,nodead][noform] 精灵之火;[@mouseover,harm,nodead][] 精灵之火 (野性)

[noform]没形态即人形态，比起 [form:1/3] 锁定为 1 熊或 3 豹形态要节约 2 字符

如果你有鼠标指向要求的话像主贴正式服的宏加入鼠标判定

如果你只要基础功能的简化宏也可以自行添加或取消这些额外的判定，以后的内容请自行加减，多个版本不一一举例，选最多功能，智能化，最精简的语法为初衷的版本

小德境界马戏团杂技，变形大师

技能整合宏，精简宏的数量，节约宏栏位

一般用于不同形态下才能使用的不同的需要做宏添加判定的技能

例如爪击 or 挥击因为需要用到 / startattack 自动攻击而做了宏，这样会占用 2 个栏位，把他们整合在一起就只有一个栏位

(自动攻击注释：用近战技能会自动选目标，没资源的时候也会选目标，只在放出来的时候才会开启自动攻击，特别是用怒气职业建议常用技能添加自动攻击，以便无怒气施放技能的时候也会开启自动攻击平砍)

#showtooltip

/startattack

/ 施放 [form:3] 爪击; 挥击

进阶：为了立马击杀掉萨满的图腾，做了瞬发技能的指向宏，当你鼠标划过去就瞬间秒掉了图腾，从而节约选定的操作，这种简化在德的技能上尤其明显，比如战士在旋风斩或顺批的时候旁边目标闪避了，你可以划过去用一键切换姿态带指向功能的压制宏对闪避目标压制

(猫贼会丢豆子建议手动，下面只改了熊挥击)

#showtooltip

/startattack

/ 施放 [form:3] 爪击;[@mouseover,harm,nodead][] 挥击

/startattack 自动攻击命令的使用：用近战技能会自动选择最近的目标 (跟 TAB 一样，仅选择)，近战技能施放成功的同时会开启自动攻击。建议怒气职业常用技能添加自动攻击，以便无怒气施放技能的时候也会开启自动攻击平砍。潜行技能在找面向时会多次尝试施放，不要添加自动攻击避免破潜行

小德取消其他形态同时变形 > 潜行 > 技能，组合键取消潜行

#showtooltip

/cancelaura [mod] 潜行

/cancelform [form:1/2/4]

/ 施放 [noform:3] 猎豹形态;[nostealth] 潜行; 毁灭

小德取消其他形态同时变形同时潜行 > 技能，组合键取消潜行

#showtooltip

/cancelaura [mod] 潜行

/cancelform [form:1/2/4]

/ 施放 [noform:3] 猎豹形态

/ 施放 [nostealth] 潜行; 毁灭

整合有 GCD 和无 GCD 技能一键施放简化操作

无 GCD 技能：部分打断，冲锋拦截，变形姿态，影遁贼德潜行

有 GCD 和无 GCD 技能的组合一键完成简化操作;

比如切之态同时冲锋并自动判定战斗中切狂暴拦截，防御姿态同时盾击，小德变形同时潜行，这些操作都只需要一键

德整合 1：潜行 > 潜行技能 > 普通技能，组合键取消潜行，爪机和挥击整合

#showtooltip

/cancelaura [mod] 潜行

/ 施放 [form:3,nocombat,nostealth] 潜行;[form:3,stealth] 毁灭;[form:3] 爪击;[@mouseover,harm,nodead][] 挥击

德整合 2-1(四键)：. 取消其他形态同时变形 > 潜行 > 潜行技能 > 普通技能，组合键取消潜行

#showtooltip

/cancelaura [mod] 潜行

/cancelform [form:1/2/4]

/ 施放 [noform:3] 猎豹形态;[nocombat,nostealth] 潜行;[stealth] 毁灭; 撕碎

德整合 2-2(三键). 取消其他形态同时变形同时潜行 > 潜行技能 > 普通技能，ctrl 或 alt 取消潜行

* 变形作为起手技能加入额外的下坐骑，起立

* 增加 shift 覆盖变形; 加入此开关把宏当作常规变形减少重复功能节省键位，点了激怒覆盖变形刷能量; 另秒解定身减速

#showtooltip

/stand

/dismount

/cancelaura [mod] 潜行

/cancelform [mod:shift][form:1/2/4]

/ 施放 [noform:3] 猎豹形态

/stopmacro [mod]

/ 施放 [nocombat,nostealth] 潜行;[stealth] 毁灭; 撕碎

一键冲锋 (德)：取消其他形态同时变熊同时冲锋 (自动攻击在变形之后，如果没有 CD 可以增加怒气)

* 变形作为起手技能加入额外的下坐骑，起立

* 增加组合键覆盖变形; 加入此开关把宏当作常规变形减少重复功能节省键位，避免点了激怒天赋变形有资源导致同时误放技能; 另秒解定身减速

#showtooltip [@mouseover,harm,nodead][] 野性冲锋

/stand

/dismount

/cancelform [mod][form:2/3/4]

/ 施放 [noform:1] 巨熊形态

/startattack

/stopmacro [mod]

/ 施放 [@mouseover,harm,nodead][] 野性冲锋

一键重击：取消其他形态同时变熊同时重击 (自动攻击在变形之后，如果没有 CD 可以增加怒气)

* 变形作为起手技能加入额外的下坐骑，起立

* 增加组合键覆盖变形; 加入此开关把宏当作常规变形减少重复功能节省键位，避免点了激怒天赋变形有资源导致同时误放技能; 另秒解定身减速

#showtooltip [@mouseover,harm,nodead][] 重击

/stand

/dismount

/cancelform [mod][form:2/3/4]

/ 施放 [noform:1] 巨熊形态

/startattack

/stopmacro [mod]

/ 施放 [@mouseover,harm,nodead][] 重击

盗贼境界审时度势，致命一击

盗贼脱战潜行 + 换匕首，潜行偷窃 + 伏击 (或偷袭)，战斗切割 (或背刺)+ 换剑锤

* 潜行作为起手技能加入额外的下坐骑，起立

三键版

潜行 + 换匕首 > 偷窃 + 伏击 > 切割 + 换剑锤，组合键取消潜行

#showtooltip [nocombat,nostealth] 潜行;[stealth] 伏击; 切割

/stand

/dismount

/cancelaura [mod] 潜行

/ 施放 搜索

/stopcasting

/ 施放 [nocombat,nostealth] 潜行;[stealth] 伏击; 切割

/equipslot [stealth]16 匕首名字

/equipslot [nostealth]16 剑锤名字

二键版

潜行 + 换匕首 > 偷窃 + 伏击 + 换剑锤，组合键取消潜行

#showtooltip [nocombat,nostealth] 潜行; 伏击

/stand

/dismount

/cancelaura [mod] 潜行

/ 施放 搜索

/stopcasting

/ 施放 [nocombat,nostealth] 潜行; 伏击

/equipslot 16 匕首名字

/equipslot 16 剑锤名字

盗贼还击 + 邪恶 + 投掷 (弓弩枪)+ 换武器

换武器放在技能前需要按 2 次影响手感，为了避免第一次伤害由匕首造成所以换武器由上面的潜行宏完成，这里加入作为检查保险

#showtooltip 邪恶攻击

/startattack

/ 施放 还击

/ 施放 邪恶攻击

/ 施放 投掷

/equipslot 16 剑锤名字

凿击，闷棍，致盲，忏悔，诸如此类技能的偷刀宏 (放技能时你的普攻没 CD 就能偷一刀，原理是伤害在效果之前产生不会打破控制。此类技能施放成功本就会停止攻击，加上 / stopattack 停止攻击防止因连按再次自动攻击，在下次普攻 CD 就绪之前都不会因连按而攻击)

#showtooltip

/startattack

/stopattack

/ 施放 [@mouseover,harm,nodead][] 致盲

地板技能一键施法：按 shtft 脚下 > 按 ctrl 或 alt 鼠标瞄准后点击 > 鼠标瞄点位置 (地板类技能通用，烈焰风暴，冰火箭雨，照明弹，手雷)

#showtooltip

/startattack

/ 施放 [mod:shift,@player][mod] [@cursor] 手雷

猎人境界人宠合一，活用陷阱

添加! 防止复按取消自动射击，所有包含自动射击的宏都应加!

* 自动射击也会随着技能施放自动打开，包括超出距离未能施放成功的近战技能，技能必须是就绪能施放状态才会自动打开

/ 施放 ! 自动射击

指向多重，自动射击，宝宝攻击 > 目标多重，自动射击，宝宝攻击; 绑定猛禽

* 技能没好也会自动射击，鼠标指向点图腾，近战施放猛禽，猎人远近攻击不共 CD，保持指向对远处目标攻击同时对近身目标近战攻击，利用这个命令最大化资源节省切目标操作量; 可如法炮制其他技能，控宠命令不宜过多使用导致其跑在路上

#showtooltip [@mouseover,harm,nodead][] 多重射击

/ 施放 猛禽一击

/startattack

/petattack [@mouseover,harm,nodead];

/ 施放 [@mouseover,harm,nodead][]! 自动射击

/ 施放 [@mouseover,harm,nodead][] 多重射击

* 猎人的近战和射击不是共 CD，就是说猎人的最高 DPS 是保持远近攻击的 CD，添加这个命令的宏不用来回切目标节省操作量，或者临时射点什么，但是谁会去记这些奇怪的条件还熟练运用呢?

/ 施放 [@mouseover,harm,nodead][]! 自动射击

猎人自动守护：队伍脱战豹群 > 单人脱战猎豹 > 战斗雄鹰; 组合键灵猴，野兽 (! 防止取消)

#showtooltip

/startattack

/ 施放 [mod:alt]! 灵猴守护;[mod]! 野兽守护;[combat]! 雄鹰守护;[group]! 豹群守护;! 猎豹守护

猎人主动守护：猎豹雄鹰切换; 组合键灵猴，野兽，豹群

#showtooltip

/startattack

/ 施放 [mod:alt]! 灵猴守护;[mod:ctrl]! 野兽守护;[mod]! 豹群守护

/castsequence 猎豹守护, 雄鹰守护

猎人一键召唤喂养，所有宠物类型和相应食物

#showtooltip

/stand

/dismount

/ 施放 [pet] 喂养宠物; 召唤宠物

/use [pet: 豹 / 狼 / 迅猛龙] 烤鹌鹑;[pet: 蝙蝠] 风干的香蕉;[pet: 熊 / 猩猩 / 野猪 / 风蛇] 魔法甜面包

死了复活，没有尸体按组合键复活 > 活着解散 > 没有召唤; 跟随保持 10 码解散范围

#showtooltip

/petfollow

/startattack

/ 施放 [mod] [@pet,dead] 复活宠物;[pet] 解散野兽; 召唤宠物

四合一究极宠物宏 (前两个宏的整合)：死了复活，没有尸体按 ctrl 复活> 按 shift 喂养>活着解散>没有召唤

#showtooltip

/stand

/dismount

/petfollow

/startattack

/ 施放 [mod:ctrl] [@pet,dead] 复活宠物;[mod:shift] 喂养宠物;[pet] 解散野兽; 召唤宠物

/stopmacro [mod:ctrl]

/ 施放 [pet: 豹 / 狼 / 迅猛龙] 烤鹌鹑;[pet: 蝙蝠] 风干的香蕉;[pet: 熊 / 猩猩 / 野猪 / 风蛇] 魔法甜面包

*“/stopmacro [mod:ctrl]” 按 ctrl 停止下面内容，下面内容是喂食功能的下半部分按类型选择食物，如果没有按 shift 施放喂养技能而下半部分选择食物依然生效会出现食物被自己吃了的情况，所以用终止命令来区分开

* 如果条件单一应该单独加入 “mod:shift” 条件而不使用终止宏，实际像这样 “[mod:shift,pet: 豹 / 狼 / 迅猛龙] 烤鹌鹑”和上面的 shift 喂养技能同步运行，但由于这个宏的种类判定有三个需要加三个 “mod:shift,” 就达到了 30 字符，不如终止宏“/stopmacro [mod:ctrl]”21 个字符简约且能匹配下面的无限种类扩展

治疗宠物，组合键低级治疗解 DEBUFF; 防止中断引导

#showtooltip

/startattack

/ 施放 [mod,nochanneling: 治疗宠物]治疗宠物 (等级 1);[nochanneling: 治疗宠物] 治疗宠物

天眼查

#showtooltip

/stand

/dismount

/ 施放 [@cursor] ! 鹰眼术

战斗假死 > 影遁陷阱，脱战影遁陷阱，组合键其他影遁陷阱

#showtooltip

/petfollow

/ 施放 [combat] 假死

/ 施放 [mod:alt] 冰霜陷阱;[mod:ctrl] 献祭陷阱;[mod] 爆炸陷阱; 冰冻陷阱

/ 施放 ! 影遁

/ 施放 ! 潜伏

点评：宠物存活情况假死后主人脱战宠物还在战斗，宏判定宝宝还在战斗所不能用判定脱战来施放陷阱，只能另起一行同时施放陷阱

战士境界武器大师，三系战神

一键冲锋 or 拦截，1. 姿态图标版无法监视技能图标 CD 和距离：切战斗姿态冲锋 > 战斗中切狂暴拦截

这个版本显示的姿态图标，切换姿态后才显示技能，因为同时施放的缘故，基本看不到显示技能的情况，这样就失去了监视技能图标 CD 和超出距离变红的功能

#showtooltip

/ 施放 [combat] 狂暴姿态; 战斗姿态

/startattack

/ 施放 [combat,@mouseover,harm,nodead][combat] 拦截;[@mouseover,harm,nodead][] 冲锋

一键冲锋 or 拦截，2. 技能图标版监视技能 CD 和距离：功能同上，区别在有图标距离显示，起手技能应添加起立下马命令

这个版本脱战显示冲锋，战斗拦截，指向和目标都有距离判定

#showtooltip [combat,@mouseover,harm,nodead][combat] 拦截;[@mouseover,harm,nodead][] 冲锋

/stand

/dismount

/ 施放 [combat] 狂暴姿态; 战斗姿态

/startattack

/ 施放 [combat,@mouseover,harm,nodead][combat] 拦截;[@mouseover,harm,nodead][] 冲锋

战神：顾名思义，三合一自动切姿态换武器化身为神，可惜你只能选择其中之一而且只有 10 秒钟真男人萎靡一整天

* 可以用作一键切换剑盾和防御姿态，坐骑上可以切换武器和姿态，加入自动攻击会导致下坐骑，已是剑盾会尝试施放技能也会下坐骑; 这个附属功能是方便坐骑上变得更硬方便冲破障碍方便赶路

#showtooltip [mod:alt] 反击风暴;[mod] 鲁莽; 盾墙

/ 施放 [mod:alt] 战斗姿态;[mod] 狂暴姿态; 防御姿态

/equip [mod] 双手武器

/equipslot [nomod]16 单手武器

/equip [nomod] 盾牌名字

/ 施放 [mod:alt] 反击风暴;[mod] 鲁莽; 盾墙

一键盾击：狂暴切防御，战斗同时盾击; 如需换武器连按 2 次施放技能

#showtooltip [@mouseover,harm,nodead][] 盾击

/ 施放 [form:3] 防御姿态

/startattack

/equipslot 16 单手武器

/equip 盾牌名字

/ 施放 血性狂暴

/ 施放 [@mouseover,harm,nodead][] 盾击

一键缴械：切防御同时缴械

#showtooltip [@mouseover,harm,nodead][] 缴械

/ 施放 防御姿态

/startattack

/ 施放 血性狂暴

/ 施放 [@mouseover,harm,nodead][] 缴械

一键拳击：切狂暴同时拳击

#showtooltip [@mouseover,harm,nodead][] 拳击

/ 施放 狂暴姿态

/startattack

/ 施放 血性狂暴

/ 施放 [@mouseover,harm,nodead][] 拳击

一键旋风：切狂暴同时旋风斩; 如需换武器连按 2 次施放技能

#showtooltip 旋风斩

/ 施放 狂暴姿态

/equip 双手武器

/startattack

/ 施放 旋风斩

一键压制：战斗直接压制，防御狂暴切武器同时压制; 如需换武器连按 2 次施放技能

#showtooltip [@mouseover,harm,nodead][] 压制

/ 施放 战斗姿态

/equip 双手武器

/startattack

/ 施放 [@mouseover,harm,nodead][] 压制

一键断筋：武器直接断筋，防御切狂暴同时断筋

#showtooltip [@mouseover,harm,nodead][] 断筋

/ 施放 [form:2] 狂暴姿态

/startattack

/ 施放 [@mouseover,harm,nodead][] 断筋

一键斩杀：战斗直接斩杀，防御切狂暴同时压制; 默认换武器

#showtooltip [@mouseover,harm,nodead][] 斩杀

/ 施放 [form:2] 狂暴姿态

/equip 双手武器

/startattack

/ 施放 [@mouseover,harm,nodead][] 斩杀

一键横扫：切姿态同时普攻同时血性狂暴同时横扫 (普攻就绪时才能一键施放，依靠普攻的怒气同时横扫，普攻 CD 时需要按 2 次，因为血性狂暴返还的怒气有延迟)

#showtooltip 横扫攻击

/ 施放 战斗姿态

/equip 旋风之斧

/startattack

/ 施放 血性狂暴

/ 施放 横扫攻击

注意 1：战士和熊一样自动攻击加怒气，应该在切换姿态之后才自动攻击增加当前姿态的怒气，保证不易出，也保证变熊或切姿态后当前姿态有额外的怒气

举例：如果你有 25 点怒气切姿态旋风斩，你也正好普攻没有 CD，切换姿态后普攻加了 10 点怒气，旋风斩用掉 25 点，这时候你还剩 10 点，而不是先攻击在随着切姿态浪费掉，在 PVE 无间断的普攻这个逻辑细节可能没有用，不过 PVP 经常有打不到人普攻无 CD 的状态是很有用的，养成良好的逻辑习惯

战德还有很多可以做一键切姿态的技能，缴械，嘲讽，盾墙，惩戒痛击，狂暴之怒，精灵之火，诸如此类等等，如法炮制

注意 2：战士的姿态不会取消，德鲁伊的形态重复按会取消变人，他们涉及姿态或形态的宏有一定差异

比如战士有拳击，缴械这种指定单一姿态施放的技能只需要 “/ 施放 XX 姿态” 即可，而盾击，断筋，斩杀这种双姿态都能施放的技能要加入“/ 施放 [form：X]XX 姿态”来判定，比如你在防御姿态去按断筋就会切到指定的战斗或狂暴之态施放

德鲁伊的技能都是单形态施放，不像战士有双姿态能施放的技能，比如要变熊冲锋第一行先取消别的形态“/cancelform [form:2/3/4]”取消 234 形态，第二行 “/ 施放 [noform:1] 巨熊形态”变熊，其中 [noform:1] 防止重复按键取消形态

布甲魔杖射击指向目标，宝宝攻击指向目标 > 射击目标，宝宝攻击目标

#showtooltip

/startattack

/petattack [@mouseover,harm,nodead];

/ 施放 [@mouseover,harm,nodead][]! 射击

牧师

友方优先指向驱散复活同时解除形态，组合键驱自己或复活 (对释放尸体用); 敌方优先指向沉默

#showtooltip

/stopcasting [@mouseover,harm] [harm]

/cancelform [mod] [@mouseover,help] [help]

/ 施放 [mod:alt,@player][@mouseover,help,nodead][help,nodead] 驱除疾病;[mod] [@mouseover,help,dead] [help,dead] 复活术;[@mouseover,harm,nodead][] 沉默

优先指向驱散，组合键驱自己

#showtooltip

/ 施放 [mod,@player][@mouseover,exists,nodead][] 驱散魔法

不打断鞭子引导

#showtooltip [@mouseover,harm,nodead][] 精神鞭笞

/startattack

/ 施放 [@mouseover,harm,nodead,nochanneling: 精神鞭笞][nochanneling: 精神鞭笞] 精神鞭笞

加入! 防止误触取消形态，取消! 在施放暗影形态 1.5 秒后能直接按下取消形态; 按组合键取消形态

PS：起立同时暗影形态，坐骑上可以取消形态遂取消了下坐骑命令

#showtooltip

/stand

/startattack

/cancelform [mod]

/ 施放 暗影形态

组合键 1 级痛 > 鼠标指向 > 目标 (1 级追杀刷晕，众所周知暗牧没法留人)

#showtooltip

/stand

/dismount

/startattack

/ 施放 [mod,@mouseover,help,nodead][mod] 暗言术：痛 (等级 1);[@mouseover,help,nodead][] 暗言术：痛

骑士

友方优先指向复活驱散，组合键复活 (对释放尸体用) 或驱自己

#showtooltip

/ 施放 [mod:shift] [@mouseover,help,dead] [help,dead] 救赎;[mod,@player][@mouseover,help,nodead][] 清洁术

忏悔优先指向

#showtooltip

/startattack

/stopattack

/ 施放 [@mouseover,harm,nodead][] 忏悔

法师，解诅咒和反制 +?

法师

组合键 1 级寒冰 > 鼠标指向 > 目标 (1 级 1-1.5 秒读条快速减速)

#showtooltip

/startattack

/ 施放 [mod,@mouseover,harm,nodead][mod] 寒冰箭 (等级 1);[@mouseover,harm,nodead][] 寒冰箭

不打断暴风雪引导：按 shtft 鼠标瞄点位置 1 级雪 > 按 ctrl 或 alt 鼠标瞄准后点击 > 鼠标瞄点位置

#showtooltip 暴风雪

/startattack

/ 施放 [mod:shift,@cursor]暴风雪 (等级 1);[mod][@cursor,nochanneling: 暴风雪] 暴风雪

小德，解中毒和战复 + 缠绕，解诅咒和休眠 + 安抚

骑士，解净化和复活 +?

队列施放，优先指向，5 秒重置

#showtooltip

/dismount

/stand

/startattack

/castsequence [@mouseover,harm,nodead][]reset=5 献祭, 痛苦诅咒, 腐蚀术

![](https://olimg.3dmgame.com/uploads/images/raiders/20210127/1611710146_721072.jpg)

**各职业整合宏**

组合键适用整合频率不高的非反应类型技能道具，通常 shift,alt 比 ctrl 好按一些，自己有设置组合键习惯的可以去掉相应的按键避免冲突，盗贼 4 种毒左右键点击八合一，能起到整合作用又不影响操作情况就算二合一也有意义

作为思维扩展包含很多各职业各种族延伸功能，一点思路请自行调节，不需要的功能请自行加减

宏能实现 30 个以上的功能整合在一个键位吗? 受限于 255 字符可以用宏扩展插件突破，但这样做违背宏简化操作的根本，也无法监视图标的状态：3 个组合键 + 正常键，姿态，敌我，死亡，战斗，环境等判定以 4 的倍数扩展，3 个组合键 + 正常键的左右键分别点击，已经突破 30 个技能判定了。做宏要打牢基础，不忘初衷，千万不要掉入华而不实的陷阱，思维可以延伸，但要切合实际

上毒药四合一，左键主手右键副手

#showtooltip

/ 施放 [mod:alt] 致命药膏; [mod:ctrl] 麻痹毒药;[mod] 致残毒药; 速效毒药

/use [button:1]16; [button:2]17

法师 BUFF 四合一

#showtooltip

/ 施放 [mod:alt] 魔甲术;[mod:ctrl] 奥术光辉;[mod] 奥术智慧; 冰甲术

传送三合一

#showtooltip

/ 施放 [mod:alt] 传送：暴风城;[mod] 传送：达纳苏斯; 传送：铁炉堡

专业四合一

#showtooltip

/ 施放 [mod:alt] 烹饪;[mod:ctrl] 急救;[mod] 工程学; 炼金术

吃喝用上药五合一; 整合取消形态，影遁，潜行，宝宝潜行

#showtooltip

/cancelform

/ 施放 [mod:alt] 用道具;[mod:ctrl] 吃烹饪;[mod] 上 BUFF;[combat] 药; 吃食物

/stopmacro [mod:shift/ctrl]

/ 施放 [combat]XX 治疗石

/ 施放 喝水

/ 施放 影遁

/ 施放 潜行

/ 施放 ! 潜伏

全职业版 (除暗牧)，其他功能同上，加入鼠标指向上 BUFF，鼠标右键吃蓝药，取消寒冰屏障

#showtooltip

/cancelform

/cancelaura 寒冰屏障

/ 施放 [mod:ctrl] 吃烹饪;[mod:shift,@mouseover,help,nodead][mod:shift] 上 BUFF;[button:2]XX 法力药水;[combat]XX 治疗药水; 吃食物

/stopmacro [mod:shift][button:2]

/ 施放 [combat]XX 治疗石

/ 施放 喝水

/ 施放 影遁

/ 施放 潜行

/ 施放 ! 潜伏

盗贼版 (BUFF 栏改毒药)

#showtooltip

/ 施放 [mod:ctrl] 吃烹饪;[mod:shift] 毒药;[combat]XX 治疗药水; 吃食物

/stopmacro [mod:shift]

/ 施放 [combat]XX 治疗石

/ 施放 影遁

/ 施放 潜行

暗牧版 (不取消形态)

#showtooltip

/ 施放 [mod:ctrl] 吃烹饪;[mod:shift,@mouseover,help,nodead][mod:shift] 真言术：韧;[button:2]XX 法力药水;[combat]XX 治疗药水; 吃食物

/stopmacro [mod:shift][button:2]

/ 施放 [combat]XX 治疗石

/ 施放 喝水

/ 施放 影遁

全职业 (除暗牧) 上坐骑整合宏：同时起立，下马，停止攻击，宝宝跟随，宠物潜行; 组合键种族技能

小德额外功能：战斗取消变形同时变水栖或旅行，秒切姿态解定身减速; 小宠物

按 ctrl 或 alt 取消形态同时影遁，按 shift 取消形态变形旅行形态，用于脱战短距离旅行

#showtooltip

/stand

/dismount

/petfollow

/stopattack

/cancelform [combat][nocombat]

/ 施放 精龙的蛋 (需要小动物添加这一行在坐骑前面，小德添加在取消形态下面变形前面)

/ 施放 [swimming] 水栖形态;[mod:shift][combat] 旅行形态

/ 施放 [mod] 感知; 黑马缰绳

/ 施放 [mod]! 影遁; 条纹霜刃豹缰绳

/ 施放 [mod]! 潜伏

/ 施放 召唤地狱战马

/ 施放 召唤军马

单职业的话在上面基础上修改，小德和暗牧形态冲突不能共存需单独做一个

小德：取消形态同时变水栖或旅行，秒切姿态解定身减速;

按 ctrl 取消形态同时影遁，按 shift 或 alt 取消形态同时上坐骑

#showtooltip

/stand

/dismount

/cancelform [combat][nocombat]

/ 施放 [mod:ctrl]! 影遁;[mod] 条纹霜刃豹缰绳;[swimming] 水栖形态; 旅行形态

暗牧：去掉取消形态

#showtooltip

/stand

/dismount

/stopattack

/ 施放 [mod]! 影遁; 条纹霜刃豹缰绳

/ 施放 [mod] 感知; 黑马缰绳

全职业取消宏

/cancelform

/cancelaura 魔法抑制

/cancelaura 魔法增效

/cancelaura 法力护盾

一键标记宏 (数字改人名)

/ 脚本 SR=SetRaidTarget;TG="target"

/cleartarget

/ 目标 1

/script SR(TG,1)

/cleartarget

/ 目标 2

/script SR(TG,2)

/cleartarget

/ 目标 3

/script SR(TG,3)

/cleartarget

/ 目标 4

/script SR(TG,4)

/cleartarget

/ 目标 5

/script SR(TG,5)

![](https://olimg.3dmgame.com/uploads/images/raiders/20210127/1611710146_170006.jpg)